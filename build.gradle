plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "com.gradleup.shadow" version "8.3.6"
}

group = 'fr.zeyx'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "mvn-wesjd-net"
        url = "https://mvn.wesjd.net/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
    implementation("net.wesjd:anvilgui:1.10.11-SNAPSHOT")
}

tasks {
    runServer {
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

/**
 * Shadow (shading + relocation)
 */
tasks.shadowJar {
    archiveClassifier.set("")
    relocate("net.wesjd.anvilgui", "fr.zeyx.murder.anvilgui")
}

/**
 * Chemin vers le dossier plugins de ton serveur
 */
def serverPlugins = file("../mcpz-murder/plugins")

/**
 * Copie automatiquement le jar dans le serveur
 */
tasks.register("copyToServer") {
    dependsOn(tasks.named("shadowJar"))
    doLast {
        copy {
            from(tasks.named("shadowJar").flatMap { it.archiveFile })
            into(serverPlugins)
        }
    }
}

/**
 * build d√©clenche automatiquement shadowJar + copie
 */
tasks.build {
    dependsOn("copyToServer")
}
