buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "com.gradleup.shadow" version "8.3.6"
    id "org.hidetake.ssh" version "2.12.0"
}

group = 'fr.zeyx'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "mvn-wesjd-net"
        url = "https://mvn.wesjd.net/"
    }
    maven {
        name = "citizens-repo"
        url = "https://maven.citizensnpcs.co/repo"
    }
    maven {
        name = "codemc-releases"
        url = "https://repo.codemc.io/repository/maven-releases/"
    }
    maven {
        name = "codemc-snapshots"
        url = "https://repo.codemc.io/repository/maven-snapshots/"
    }
    maven {
        name = "glaremasters-public"
        url = "https://repo.glaremasters.me/repository/public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
    compileOnly("com.google.code.gson:gson:2.11.0")
    compileOnly("net.citizensnpcs:citizensapi:2.0.41-SNAPSHOT")
    implementation("net.wesjd:anvilgui:1.10.11-SNAPSHOT")
    implementation("fr.mrmicky:fastboard:2.1.5")
    implementation("dev.triumphteam:triumph-gui:3.1.13")
}

tasks {
    runServer {
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

/**
 * Shadow
 */
tasks.shadowJar {
    archiveClassifier.set("")
    relocate("net.wesjd.anvilgui", "fr.zeyx.murder.anvilgui")
    relocate("fr.mrmicky.fastboard", "fr.zeyx.murder.fastboard")
}

/**
 * SFTP deploy
 */
def envOrProp = { String propName, String envName ->
    def value = project.findProperty(propName)
    if (value == null || value.toString().trim().isEmpty()) {
        value = System.getenv(envName)
    }
    return value
}

def deployHost = envOrProp("deployHost", "DEPLOY_HOST")
def deployUser = envOrProp("deployUser", "DEPLOY_USER")
def deployPassword = envOrProp("deployPassword", "DEPLOY_PASSWORD")
def deployPath = envOrProp("deployPath", "DEPLOY_PATH")
def deployPortRaw = envOrProp("deployPort", "DEPLOY_PORT")
def deployStrictHostKeyCheckingRaw = envOrProp("deployStrictHostKeyChecking", "DEPLOY_STRICT_HOST_KEY_CHECKING")
def deployPort = 22
if (deployPortRaw) {
    try {
        deployPort = deployPortRaw.toString().toInteger()
    } catch (Exception ignored) {
        throw new GradleException("deployPort/DEPLOY_PORT must be an integer. Got: ${deployPortRaw}")
    }
}
def strictHostKeyChecking = !(deployStrictHostKeyCheckingRaw?.toString()?.toLowerCase() in ["0", "false", "no", "off"])

ssh.settings {
    if (!strictHostKeyChecking) {
        knownHosts = allowAnyHosts
    }
}

tasks.register("deploySftp") {
    dependsOn(tasks.named("shadowJar"))
    doLast {
        def missing = []
        if (!deployHost) missing << "deployHost/DEPLOY_HOST"
        if (!deployUser) missing << "deployUser/DEPLOY_USER"
        if (!deployPassword) missing << "deployPassword/DEPLOY_PASSWORD"
        if (!deployPath) missing << "deployPath/DEPLOY_PATH"
        if (!missing.isEmpty()) {
            throw new GradleException("Missing SFTP settings: ${missing.join(', ')}")
        }

        def jarFile = tasks.named("shadowJar").get().archiveFile.get().asFile
        ssh.run {
            session(host: deployHost, user: deployUser, port: deployPort, password: deployPassword) {
                put from: jarFile, into: deployPath
            }
        }
    }
}

tasks.build {
    dependsOn("deploySftp")
}
